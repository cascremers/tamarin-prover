dnl at_most_of macro
dnl
dnl Cas Cremers
dnl December 2017
dnl
dnl Generates Tamarin restrictions that ensure that certain actions only
dnl occur at most N times, for N >= 0. Requires as arguments N, the
dnl action fact name, and the arity of the action fact. 
dnl
dnl   at_most_of(MaxCount, ActionFactName, ActionFactArity)
dnl
dnl Example:
dnl
dnl   at_most_of(2,testFact,3)
dnl
dnl Target: Tamarin source files that will be pre-processed by m4. Assumes that m4
dnl quotes are redefined in the following way:
dnl
dnl   changequote(<!,!>)
dnl   changecom(<!/*!>, <!*/!>)
dnl
dnl TODO: 
dnl * Document better
dnl
dnl
dnl ------------------------------------------------------------------
dnl
dnl Generic sequence
dnl m4_sequence(start,end,prefix,postfix,separator)
dnl $1: start
dnl $2: end
dnl $3: prefix
dnl $4: postfix
dnl $5: separator
define(<!m4_sequence!>, <!ifelse(eval($1 < $2),
1, <!$3$1$4$5<!!>m4_sequence(incr($1),<!$2!>,<!$3!>,<!$4!>,<!$5!>)!>, <!$3$1$4!>)!>)dnl
dnl
dnl Generic block
dnl m4_AMO_block(start,end,factArity,pre,mid,post,cnt)
dnl $1: start
dnl $2: end
dnl $3: factArity
dnl $4: pre
dnl $5: mid
dnl $6: post (line), except for last line
dnl $7: cnt
dnl if cnt is non-empty, add counter and then (if cnt != '') counter
dnl and cnt.
define(<!m4_AMO_block!>, <!ifelse(eval($1 <= $2),
1,
<!$4<!!>m4_sequence(1,<!$3!>,<!a<!!>$1<!!>v!>,<!!>,<!$5!>)$6<!!>ifelse($7,,,$1<!!>ifelse(eval($1 < $2),1,<!$7!>))<!!> 
      m4_AMO_block(incr($1),<!$2!>,<!$3!>,<!$4!>,<!$5!>,<!$6!>,<!$7!>)!>, <!!>)!>)dnl
dnl
dnl Generate timepoint order
dnl m4_AMO_ordertime(start,end)
define(<!m4_AMO_ordertime!>, <!ifelse(eval($1 < decr($2)),
1, <!#i$1 < #i<!!>incr($1) & <!!>m4_AMO_ordertime(incr($1),<!$2!>)!>, <!#i$1 < #i<!!>incr($1)!>)!>)dnl
dnl
dnl generate ($1 = $2) pair
define(<!m4_AMO_ineqpair!>, <!(#i$1 = #i$2)!>)
dnl
dnl generate ($1 = $1+1 ... $2) list of things with or separators
dnl $1 : rhs (current index, typically start at min+1 = incr($2))
dnl $2 : lhs (min) 
dnl $3 : rhs (max)
dnl $4 : empty means first
define(<!m4_AMO_ineqline!>, <!ifelse(eval($1 <= $3),
1, ifelse($4,1,<! | !>)m4_AMO_ineqpair($2,$1)<!m4_AMO_ineqline(incr($1),<!$2!>,<!$3!>,<!1!>)!>,<!!>)!>)
dnl
dnl all combos
dnl $1 : min
dnl $2 : max
define(<!m4_AMO_ineqform!>, <!ifelse(eval($1 < $2),
1,ifelse($3,1,<!
$4 | !>)<!!>m4_AMO_ineqline(incr($1),<!$1!>,<!$2!>)<!m4_AMO_ineqform(incr($1),<!$2!>,<!1!>,<!$4!>)!>,<!!>)!>)
dnl
dnl The axiom generator
dnl at_most_of(maxOccurrence,factName,factArity)
define(at_most_of,<!
restriction At_most_$1_of_$2:
"All m4_sequence(1,<!incr($1)!>,#i,<! !>) 
      m4_AMO_block(1,<!incr($1)!>,<!$3!>,,<! !>).
      m4_AMO_block(1,<!incr($1)!>,<!$3!>,<!$2(!>,<!,!>,<!)@#i!>,<! &!>)dnl
==> ifelse($1,0,<!F!>,<!( m4_AMO_ineqform(1,<!incr($1)!>,0,<!         !>) )!>)"dnl
!>)dnl
